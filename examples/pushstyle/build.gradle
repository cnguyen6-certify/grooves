import org.jooq.SQL

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.2" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath 'nu.studer:gradle-jooq-plugin:2.0.9'
        classpath 'com.h2database:h2:1.4.196'
        classpath "gradle.plugin.com.boxfuse.client:flyway-release:4.2.0"
    }
}

apply plugin: 'kotlin'
apply plugin: 'nu.studer.jooq'
apply plugin: "org.flywaydb.flyway"

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.2" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}"
    compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    compile 'com.google.inject:guice:4.1.0'
    compile 'com.google.guava:guava:23.4-jre'

    compile 'com.h2database:h2:1.4.196'
    compile 'org.jooq:jooq'

    compile project(':grooves-api')

    jooqRuntime 'com.h2database:h2:1.4.196'

    runtime 'org.slf4j:slf4j-simple:1.7.25'
    testCompile 'junit:junit:4.12'
}

sonarqube {
    properties {
        property 'sonar.moduleKey', 'com.github.rahulsom:grooves:examples-pushstyle'
    }
}

jooq {
    version = '3.10.1'
    edition = 'OSS'
    main(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = "jdbc:h2:file:${buildDir}/schema"
            user = 'sa'
            password = ''
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'grooves.example.pushstyle'
            }
        }
    }
}

flyway {
    url = "jdbc:h2:file:${buildDir}/schema"
    user = 'sa'
    password = ''
    schemas = ['public']
}

generateMainJooqSchemaSource.dependsOn 'flywayMigrate'
compileKotlin.dependsOn 'generateMainJooqSchemaSource'

checkstyleMain.source = "src/main/java"
