buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ajoberstar.github-pages'

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    asciidoctor 'com.github.spring-io:spring-asciidoctor-extensions:05bca0c665'
    asciidoctor project(':grooves-diagrams')
}

asciidoctor {
    sources {
        include 'index.adoc'
    }
    attributes 'sourcedir': rootDir.absolutePath,
            'version': project.version.toString(),
            'toc': 'left',
            'source-highlighter': 'coderay',
            'icons': 'font',
            'stylesheet': 'grooves.css'
}

asciidoctor.dependsOn ':grooves-diagrams:jar'
def env = System.getenv()

githubPages {
    repoUri = 'https://github.com/rahulsom/grooves.git'
    targetBranch = 'gh-pages'
    deleteExistingFiles = false
    pages {
        from(file("$buildDir/asciidoc/html5")) {
            into "manual/${version}"
        }
        if (env['BRANCH_NAME'] == 'master') {
            from(file("$buildDir/asciidoc/html5")) {
                into "manual/current"
            }
            from(rootProject.file('grooves-site')) {
                into '.'
            }
        }
    }
    credentials {
        username = System.getenv('GH_TOKEN')
        password = '\n'
    }
}

tasks.findByName('publishGhPages').dependsOn 'asciidoctor'
